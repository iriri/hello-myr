use std
use thread
use "chan"

const Threadc = 16

generic cantry = {r
	match r
	| `std.Ok _: -> true
	| `std.Err _ : -> false
	;;
}

const main = {
	var pool : chan.chan(uint32)[Threadc]
	var set = chan.mkset(Threadc)
	var n = 0
	var w = thread.mkwg(Threadc)
	var wp = &w

	for var i = 0; i < Threadc; i++
		pool[i] = chan.mk(i % 2 == 0 ? 1 : 0)
		var c = pool[i]
		thread.spawn({
			while (cantry(chan.send(c, i)))
				std.usleep(100000)
			;;
			thread.wgpost(wp)
		})
		chan.setadd(set, pool[i], `chan.Recv, &n)
	;;

	for var i = 0; i < 64; i++
		match chan.select(set, true)
		| `std.Ok id:
			std.put("{} {}\n", id, n)
			std.assert(id == n, "select is broken\n")
		| `std.Err _: std.die("select is broken\n")
		;;
	;;

	for var i = 0; i < Threadc; i++
		chan.close(pool[i])
	;;
	chan.setfree(set)
	thread.wgwait(&w)
	for var i = 0; i < Threadc; i++
		chan.free(pool[i])
	;;
}
